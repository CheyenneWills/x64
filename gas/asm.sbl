-title mincod: phase 2 translation from minimal tokens to 80386 code
-stitl description

* copyright 1987-2012 robert b. k. dewar and mark emmer.
* copyright 2012-2015 david shields

* this file is part of macro spitbol.

*     macro spitbol is free software: you can redistribute it and/or modify
*     it under the terms of the gnu general public license as published by
*     the free software foundation, either version 2 of the license, or
*     (at your option) any later version.

*     macro spitbol is distributed in the hope that it will be useful,
*     but without any warranty; without even the implied warranty of
*     merchantability or fitness for a particular purpose.  see the
*     gnu general public license for more details.

*     you should have received a copy of the gnu general public license
*     along with macro spitbol.  if not, see <http://www.gnu.org/licenses/>.


*  this program takes input file in minimal token form and
*  produces assembly code for intel 80386 processor.
*  the program obtains the name of the file to be translated from the
*  command line string in host(0).  options relating to the processing
*  of comments can be changed by modifying the source.

*  in addition to the minimal token file, the program requires the
*  name of a "machine definition file" that contains code specific
*  to a particular 80386 assembler.

*  you may also specify option flags on the command line to control the
*  code generation.  the following flags are processed:
*	compress	generate tabs rather than spaces in output file
*       comments        retain full-line and end-of-line comments

*  in addition to the normal minimal register complement, one scratch
*  work register, w0 is defined.  see the register map below for specific allocations.

*  this program is based in part on earlier translators for the
*  it is based in part on earlier translators for the dec vax
*  (vms and un*x) written by steve duff and robert goldberg, and the
*  pc-spitbol translator by david shields.

*  to run under spitbol:
*       spitbol -u "<file>:<machine>[:flag:...:flag]" codlinux.spt

*	reads <file>.lex	containing tokenized source code
*       writes <file>.s         with 80386 assembly code
*	also writes <file>.err	with err and erb error messages
*       parts of n.hdr  are prepended and appended to <file>.s
*	also sets flags		to 1 after converting names to upper case
*	also reads <file>.pub	for debug symbols to be declared public

*  example:
*       spitbol -u v37:dos:compress codlinux.spt


*  revision history:

        version = 'v1.12'
	rcode = '_rc_'

-eject

*  keyword initialization

	&anchor = &trim	= &dump = 1
	&stlimit = 10000000
*	&dump = 3

*  useful constants

        comment.delim = '#'
	letters = &lcase
	cs	= '# '			;* comment string
	ls	= ':'			;* label and options delimiter 
	literal_ = '$'
	nos     = '0123456789'
	tab	= char(9)


*  function definitions

*  crack parses stmt into a stmt data plex and returns it.
*  it fails if there is a syntax error.

	define('crack(line)operands,operand,char')

	define('chktrace()')
*	comregs - map minimal register names to target register names
	define('comregs(line)t,pre,word')

*  error is used to report an error for current statement

	define('error(text)')
	define('flush()')
	define('genz()')
	define('genaop(stmt)')
	define('genbop(stmt)')
        define('genlab()')
        define('genrip()')
	define('op(gopc,gop1,gop2,gop3)')
	define('opl(gopl,gopc,gop1,gop2,gop3)')
	define('genrep(op)l1,l2)')
	define('getarg(iarg)txt,typ,pre,post')
	define('getlea(iarg)txt,typ,reg,disp')
	define('getvalue(text)')
	define('ifreg(iarg)')
	define('iv(text)')
	define('memmem()t')
	define('newstmt(gopl,gopc,gop1,gop2,gop3)arg1,arg2,swap')
	define('outline(txt)')
	define('prcent(n)')
	define('prsarg(iarg)l1,l2')
	define('report(num,text)')
	define('ripinit()')
	define('tblini(str)pos,cnt,index,val,lastval')
	define('v(text)')

*  outstmt is used to send a target statement to the target code
*  output file outfile

	define('outstmt(ostmt)label,opcode,op1,op2,op3,comment,t,stmtout')

*  readline is called to return the next non-comment line from
*  the minimal input file (infile <=> lu1).   note that it will
*  not fail on eof, but it will return a minimal end statement

	define('readline()')

	os = 'unix';	ws = '64'
	options = host(0)
	
	output = 'options: ' options
*	add trailing colon so always have colon after an argument
	options = options ls
option.next		
*	ignore extraneous : in list (helps with writing makefiles)
	options ls =				:s(option.next)
	options break(ls) . option ls =	:f(options.done)
	outfile = cs ' options ' options ls

	ident(option,'unix_64')			:s(option.unix.64)
	ident(option,'xt')			:s(option.xt)
	ident(option,'trc')			:s(option.trc)

* here if unknown option

	output = "error: unknown option '" option "', translation ends."	
	&dump = 0				:(end)

option.unix.64
	os = 'unix'; 	ws = '64'		:(option.next)

option.trc
*	turn on instruction trace
	i_trace = 1				:(option.next)

option.xt
*  x_trace turns on tracing for executable instructions
	x_trace = 1				:(option.next)

options.done
	target = os '_' ws
	report(target,'target')

	filebase = "sbl"

*	cfp_b is bytes per word, cfp_c is characters per word
*       these should agree with values used in translator

	cfp_b = 8
	cfp_c = 8
	cfp_f = +cfp_b + +cfp_c
	log_cfp_b = 3
	log_cfp_c = 3
	op_w = 'q'
	op_c = 'b'
*	rel = 'rel '
	rel = ''


*	rip_mode is needed for osx. See getval()

	rip_mode = (ident(os,'osx') 1, 0)

	report('rip', rip_mode)
	ne(rip_mode) ripinit()

*	i_limit is maximum number of calls to be generated if non-zero
	i_limit = 0
*	set i_first non-zero to skip first number of instructions that would generate trace
	i_first = 1
*	will set in_executable when in part of program where executable
*	instructions may occur
	i_exec = 0

*	i_suspend is set to temporarily disable the trace.
*	initially set so emit no code until reach code section
	i_suspend = 0
*	set in_skip when should not insert trace code, else assembly errors result.
*	start with skip on, turn off when see first start of code.
	i_skip = 1
*	skip_on and skip_off are labels indicating the start and end,
*	respectively, of sections of the code that should not be traced,
*	usually because they contain a loop instruction that won't
*	compile if too much trace code is inserted.
	skip_on = table(50)
	skip_off = table(50)

	define('skip_init(s)on,off')		:(skip_init.end)
skip_init	s break(ls) . on ls rem . off	:f(return)
	skip_on[on] = 1
	skip_off[off] = 1			:(return)
skip_init.end

*	skip_init('start:ini03')
   	skip_init('gbcol:gtarr')
*	skip_init('gtn01:gtnvr')
*	skip_init('bpf05:bpf07')
*	skip_init('scv12:scv19')
*	skip_init('exbl1:exbl2')
*	skip_init('exbl5:expan')
*	skip_init('prn17:prn18')
*	skip_init('ini11:ini13')
*	skip_init('oex13:oexp2')
*	skip_init('oex14:oexp6')
*	skip_init('bdfc1:b_efc')
*	skip_init('sar01:sar10')
*	skip_init('srpl5:srpl8')
*	skip_init('pflu1:pflu2')
*	skip_init('prpa4:prpa5')
*	skip_init('prn17:prn18')
*	skip_init('prtvl:prtt1')
*	skip_init('trim4:trim5')
*	skip_init('prnl1:prnl2')
*	skip_init('prti1:prtmi')
*	skip_init('srpl5:srpl8')

*	Opcode symbols.
*	For each hardware operation that can take byte or word operands we define two symbols that give the
*	corresponding opcode. For example, 'add.w' for 64-bit addition, and 'add.b' for 8-bit addition.

	s = 'add and cmp cmps dec imul inc lea lods mov movs movz not or pop push shl shr stos sub test xchg xor '


op_map.init
	s break(' ') . op ' ' =			:f(op_map.done)
	$(op '.w') = op 'q'			
	$(op '.b') = op 'b'			
						:(op_map.init)
op_map.done

*	The procedures 'op' and 'opl' are used to generate instructions. The first argument to opl is the
*	opcode, followed by the operands. The first operand to 'op' is the opcode, followed by the operands.
*	For binary opcodes with two operands,we write the destination operand first, in what is called 'intel' format.
*	When generating code for the GNU as assembler (gas) the operand order is switched, in what is called 'att' 
*	format. The swap_op table is used to indicate which opcodes require this swapping of the order. The actual
*	swap is carried out in the opl procedure.


*	swap_op is set if operand order is reversed in gas att syntax

	swap_op = table(100)
	s = 	'add and cmp cmps lea mov movs movz or shl shr sub test xchg xor '
swap.init
	s break(' ') . op ' ' =			:f(swap.init.done)
	swap_op[op 'b'] = swap_op[op 'q'] = 1				:(swap.init)
swap.init.done

*	Some opcodes require special handling, as both the size of the input and the output may differ, and
*	so both must be specified.

	movz.bq	=	'movzbq'

	swap_op[movz.bq] = 1

*  data structures

	data('minarg(i.type,i.text)')
	data('tstmt(t.label,t.opc,t.op1,t.op2,t.op3,t.comment)')

	sectnow = 0

*	ppm_cases gives count of ppm/err statments that must follow call to a procedure

	ppm_cases = table(50,,0)


	 p.comregs = break(letters) . pre span(letters) . word

*  exttab has entry for external procedures

	exttab = table(50)

*  labtab records labels in the code section, and their line numbers

	labtab = table(500)

*  for each statement, code in generated into three arrays of statements:

*	astmts:	statements after opcode (()+, etc.)
*	bstmts: statements before code (-(), etc)
*	cstmts: generated code proper

	astmts = array(20,'')
	bstmts = array(10,'')
	cstmts = array(20,'')

*  genlabels is count of generated labels (cf. genlab)

	genlabels = 0

*  riplabel is count of generated rip (osx rip address mode) labels

	riplabels = 0


*  initialize variables

	labcnt = outlines = nlines = nstmts = ntarget = nerrors = 0
	lastopc = lastop1 = lastop2 =
	data_lc = 0
	max_exi = 0

*  initial patterns

*  p.csparse parses tokenized line
	p.csparse = '|' break('|') . inlabel
.	'|' break('|') . incode
.	'|' break('|') . iarg1
.	'|' break('|') . iarg2
.	'|' break('|') . iarg3
.	'|' break('|') . incomment
	'|' rem . slineno

*  dispatch table

	argcase = table(100)
	argcase[01] = .getarg.c.1;	argcase[2]  = .getarg.c.2;	argcase[3]  = .getarg.c.3;
	argcase[04] = .getarg.c.4;	argcase[5]  = .getarg.c.5;	argcase[6]  = .getarg.c.6;
	argcase[07] = .getarg.c.7;	argcase[8]  = .getarg.c.8;	argcase[9]  = .getarg.c.9;   
	argcase[10] = .getarg.c.10;	argcase[11] = .getarg.c.11;	argcase[12] = .getarg.c.12;
	argcase[13] = .getarg.c.13;	argcase[14] = .getarg.c.14;	argcase[15] = .getarg.c.15;
	argcase[16] = .getarg.c.16;	argcase[17] = .getarg.c.17;	argcase[18] = .getarg.c.18;
	argcase[19] = .getarg.c.19;	argcase[20] = .getarg.c.20;	argcase[21] = .getarg.c.21; 
	argcase[22] = .getarg.c.22;	argcase[23] = .getarg.c.23;	argcase[24] = .getarg.c.24;
	argcase[25] = .getarg.c.25;	argcase[26] = .getarg.c.26;	argcase[27] = .getarg.c.27

	leacase = table(50) 
	leacase[03] = .getlea.c.3;		leacase[04] = .getlea.c.4;		leacase[09] = .getlea.c.9;   
	leacase[12] = .getlea.c.12;	leacase[13] = .getlea.c.13;	leacase[14] = .getlea.c.14
	leacase[15] = .getlea.c.15;		 

	valcase = table(50)
	valcase[18] = .getval.c.18;	valcase[19] = .getval.c.19;	valcase[20] = .getval.c.20;	
	valcase[21] = .getval.c.21;	valcase[22] = .getval.c.22

	riptable = table(500)
	rip_count = 0
	ripcase = table(50)
	ripcase[14] = .rip.c.14;	ripcase[15] = .rip.c.15;	ripcase[18] = .rip.c.18;	
	ripcase[19] = .rip.c.19;	ripcase[20] = .rip.c.20;	ripcase[21] = .rip.c.21;	
	ripcase[22] = .rip.c.22


*  pifatal maps minimal opcodes for which no a code allowed
*  to nonzero value. such operations include conditional
*  branches with operand of form (x)+

	pifatal = tblini(
.	'aov[1]beq[1]bne[1]bge[1]bgt[1]bhi[1]ble[1]blo[1]'
.	'blt[1]bne[1]bnz[1]ceq[1]cne[1]mfi[1]nzb[1]zrb[1]')

*	trace not working for mvc (x64)

	is_executable = table(100)
	s =
+       'add adi adr anb aov atn '
+	'bct beq bev bge bgt bhi ble blo blt bne bnz bod '
+       'brn bri bsw btw bze ceq chk chp cmb cmc cmp cne csc '
+       'cos ctb ctw cvd cvm dca dcv eti dvi dvr erb esw etx flc '
+       'ica icp icv ieq ige igt ile ilt ine ino iov itr jmp '
+       'jsr lch lct lcp lcw ldi ldr lei lnf lsh lsx mcb mfi mli mlr '
+       'mnz mov mti mvw mwb ngi eti ngr nzb orb plc prc psc '
+       'req rge rgt rle rlt rmi rne rno rov rsh rsx rti rtn sbi sbr '
+       'sch scp sin sqr ssl sss sti str sub tan trc wtb xob zer '
+       'zgb zrb'

*	don't trace mvc as doing so causes just 'end' to fail. sort out later. (ds 01/09/13)

is_exec.1
	s len(3) . opc ' ' =			:f(is_exec.2)
	is_executable[opc] = 1			:(is_exec.1)
is_exec.2

-stitl main program
*  here follows the driver code for the "main" program.


*  loop until program exits via g.end

*  opnext is invoked to initiate processing of the next line from
*  readline.
*  after doing this, opnext branches to the generator routine indicated
*  for this opcode if there is one.
*  the generators all have entry points beginning
*  with "g.", and can be considered a logical extension of the
*  opnext routine.  the generators have the choice of branching back
*  to dsgen to cause the thisstmt plex to be sent to outstmt, or
*  or branching to dsout, in which case the generator must output
*  all needed code itself.

*  the generators are listed in a separate section below.

*  get file name

* get definition file name following token file name, and flags.

* parse and display flags, setting each one's name to non-null value (1).

 :(flgs.skip)
flgs	flags ? ((len(1) break(';:')) . flag len(1)) |
+	 ((len(1) rem) . flag) =			:f(flgs2)
	flag = replace(flag,&ucase,&lcase)
        output = "  flag: " flag
	$flag = 1					:(flgs)

flgs.skip
flgs2

	w0_arg = minarg(8,'w0')
*  branchtab maps minimal opcodes 'beq', etc to desired
*  target instruction

	branchtab = table(10)
	branchtab['beq'] = 'je'
	branchtab['bne'] = 'jne'
	branchtab['bgt'] = 'ja'
	branchtab['bge'] = 'jae'
	branchtab['ble'] = 'jbe'
	branchtab['blt'] = 'jb'
	branchtab['blo'] = 'jb'
	branchtab['bhi'] = 'ja'

*  ismem is table indexed by operand type which is nonzero if operand type implies memory reference.

	ismem = array(30,0)
	ismem<3> = 1; ismem<4> = 1; ismem<5> = 1
	ismem<9> = 1; ismem<10> = 1; ismem<11> = 1
	ismem<12> = 1; ismem<13> = 1; ismem<14> = 1
	ismem<15> = 1

*  regmap maps minimal register name to target machine register/memory-location name.

	regmap = table(30)
	reg.xl = regmap['xl'] = '%rsi'  
	reg.xt = regmap['xt'] = '%rsp'
	reg.xr = regmap['xr'] = '%rdi'  
	reg.xs = regmap['xs'] = '%rsp'
	reg.wa = regmap['wa'] = '%rcx'  
	reg.wb = regmap['wb'] = '%rbx'
	reg.wc = regmap['wc'] = '%rdx'  
*	reg.ia = regmap['ia'] = '%rbp'
*	w0 is temp register
	reg.w0 = regmap['w0'] = '%rax'
	reg.al = '%al'		;* least-significant (low) part of w0.


* reglow maps register to identify target, so can extract 'l' part.
	reglow = table(4)
	reglow['wa'] = '%cl';	reglow['wb'] = '%bl';	reglow['wc'] = '%dl'

* real_op maps minimal real opcode to machine opcode

	real_op = table(10)
	real_op['adr'] = 'fadd'
	real_op['atn'] = 'fpatan'
	real_op['chp'] = 'frndint'
	real_op['cos'] = 'fcos'
	real_op['dvr'] = 'fdiv'
	real_op['ldr'] = 'fld'
	real_op['mlr'] = 'fmul'
	real_op['ngr'] = 'fchs'
	real_op['sbr'] = 'fsub'
	real_op['sin'] = 'fsin'
	real_op['sqr'] = 'fsqrt'
	real_op['str'] = 'fst'

*  other definitions that are dependent upon things defined in the
*  machine definition file, and cannot be built until after the definition
*  file has been read in.

*  p.outstmt examines output lines for certain types of comment contructions
	fillc	  = (ident(compress) " ",tab)
	p.outstmt = (break(fillc) . label span(fillc)) . leader
+			comment.delim rem . comment
	p.alltabs = span(tab) rpos(0)

*  strip end of comments if y

	strip_comment = (differ(comments) 'n', 'y')

        output = ~input(.infile,1) "no input file"	:s(end)

inputok 



*  associate output files.

        output = ~output(.outfile,2) "no output file"	:s(end)
outputok


* open file for compilation of minimal err and erb messages

        output = ~output(.errfile,3) "no error file"	:s(end)
err_ok

*  read in .equ file with value for symbols defined by equ opcode (this is really only needed for gas)

	output = ~input(.equfile,4) 'cannot open equ file'	:s(end)
	equ_value = table(1000)
equ.copy
	line = equfile				:f(equ.end)
	line break(' ') . key ' ' rem . val
	equ_value[key] = val			:(equ.copy)
equ.end
	endfile(4)



 :(hdrend)
*  The 'header' information, required for assembly, is copied
*  Copy the header information to the output file. The header
*  follows the 'end' statement in this program.
*  Note that the '-r' option must be given to spitbol for this to work.

	outlines = outlines + 1
	havehdr = 1
hdrcopy line = input				:f(hdrend)
	ident(line,'end')			:s(nohdr)
	outline(line)
						:(hdrcopy)
hdrend	havehdr =
nohdr

*  will have havehdr non-null if more remains to copy out at end.

*  read in pub file if it exists.  this contains a list of symbols to
*  be declared public when encountered.

	pubtab = table(2)
	input(.pubfile,5, filebase ".pub")		:f(nopub)
	pubtab = table(101)
pubcopy	line = pubfile				:f(pubend)
	pubtab[line] = 1			:(pubcopy)
pubend	endfile(5)
nopub

						:(dsout)
  &trace = 2000
  &ftrace = 1000
*  &profile = 1
dsout
opnext	thisline = readline()
	crack(thisline)				:f(dsout)
	op_ = incode '_'


	ident(inlabel)				:s(opnext.1)

* output label of executable instruction immediately if there is one,
* as it simplifies later processing, especially for tracing.
	thislabel = inlabel (differ(inlabel) ge(sectnow,3),)
* keep the label if it is not in executable code
	lt(sectnow,5)				:s(opnext.1)
* here if in code, so output label now
* defer label processing for ent to allow emission of alignment ops for x86.
	ident(incode,'ent')			:s(opnext.1)
	outline(thislabel ls)
* set lastlabel so can check to avoid emitting duplicate label definitions
*	lastlabel = thislabel
* clear out label info once generated
	inlabel = thislabel =
opnext.1
	thislabel = inlabel (differ(inlabel) ge(sectnow,3),)
	i1 = prsarg(iarg1)
	i2 = prsarg(iarg2)
	i3 = prsarg(iarg3)
	tcomment = comregs(incomment) '} ' incode ' ' i.text(i1) ' '
.		i.text(i2) ' ' i.text(i3)
	argerrs = 0
*	differ(i_trace) ge(sectnow,5) eq(i_suspend) chktrace()
	ge(sectnow,5) chktrace()
						:($('g.' incode))
*  here if bad opcode
ds01	error('bad op-code')			:(dsout)

*  generate tokens.

ds.typerr
	error('operand type zero')		:(dsout)
-stitl comregs(line)t,pre,word
comregs
	line p.comregs =			:f(comregs1)
	word = eq(size(word),2) differ(t = regmap[word]) t
	comregs = comregs pre word		:(comregs)
comregs1 comregs = comregs line			:(return)
-stitl crack(line)
*  crack is called to create a stmt plex containing the various parts  of
* the minimal source statement in line.  for conditional assembly ops,
* the opcode is the op, and op1 is the symbol.  note that dtc is handled
*  as a special case to assure that the decomposition is correct.

*  crack prints an error and fails if a syntax error occurs.

crack   nstmts  = nstmts + 1
	op1 = op2 = op3 = typ1 = typ2 = typ3 =
	line    p.csparse			:s(return)
*  here on syntax error

	error('source line syntax error')	:(freturn)
-stitl error(text)
*  this module handles reporting of errors with the offending
*  statement text in thisline.  comments explaining
*  the error are written to the listing (including error chain), and
*  the appropriate counts are updated.

error   outline('* *???* ' thisline)
	outline('*       ' text)
.	          (ident(lasterror),'. last error was line ' lasterror)
	lasterror = outlines
	le(nerrors = nerrors + 1, 10)		:s(dsout)
        output = 'too many errors, quitting'  :(end)

-stitl genaop(stmt)
genaop

	astmts[astmts.n = astmts.n + 1] = stmt	:(return)
-stitl genbop(stmt)
genbop
	bstmts[bstmts.n = bstmts.n + 1] = stmt	:(return)

-stitl genlab()
*  generate unique labels for use in generated code
genlab	genlab = 'l_' lpad(genlabels = genlabels + 1,4,'0') :(return)

-stitl genrip()
*  generate unique labels for use in generated code
genrip	genrip = 'r_' lpad(riplabels = riplabels + 1,4,'0') :(return)

-stitl opl(gopl,gopc,gop1,gop2,gop3)
*  generate operation with label
opl	
	cstmts[cstmts.n = cstmts.n + 1] =
.		newstmt(gopl,gopc,gop1,gop2,gop3)	:(return)

-stitl newstmt(gopl,gopc,gop1,gop2,gop3)arg1,arg2,swap
newstmt

*	see if need to swap operands

	swap = swap_op[gopc]
	arg1 = (ident(swap) gop1, gop2)
	arg2 = (ident(swap) gop2, gop1)
	newstmt = tstmt((differ(gopl) gopl ls, ''),gopc,arg1,arg2,gop3)
						:(return)
-stitl op(gopc,gop1,gop2,gop3)
*  generate operation with no label
op   
	opl(,gopc,gop1,gop2,gop3)		:(return)


-stitl getarg(iarg)
getarg

*	imem is null to generate memory reference, otherwise just get address for use in 'Lea_' instruction.

	base = disp = indx = scale =

	txt = i.text(iarg)
	typ = i.type(iarg)
	i_suspend = 1
	eq(typ)					:f($(argcase[typ]))
	getarg = txt				:(getarg.done)

*	int

getarg.c.1 getarg = txt				:(getarg.done)

*	dlbl

getarg.c.2 getarg = txt				:(getarg.done)

*	wlbl, clbl

getarg.c.3
getarg.c.4 
	disp = txt				:(getarg.mem)

* elbl, plbl

getarg.c.5
getarg.c.6 getarg = txt				:(getarg.done)

*	w,x, map register name

getarg.c.7
getarg.c.8
	getarg = regmap[txt]			:(getarg.done)

*	(x), register indirect

getarg.c.9
	txt len(1) len(2) . indx		:(getarg.mem)
*	(x)+, register indirect, post increment use lea reg,[reg+cfp_b] unless reg is esp, 
*	since it takes an extra byte.  actually, lea reg,[reg+cfp_b] and add reg,cfp_b are both 
*	2 cycles and 3 bytes for all the other regs, and either could be used.

getarg.c.10
	indx = substr(txt,2,2)
	genaop(newstmt(,'addq',regmap[indx],literal_ cfp_b)) :(getarg.mem)

*	-(x), register indirect, pre decrement

getarg.c.11
	indx = substr(txt,3,2)
	genbop(newstmt(,'subq',regmap[indx],literal_ cfp_b,)) 	:(getarg.mem)

*	int(x)
*	dlbl(x)

getarg.c.12
getarg.c.13
	txt break('(') . disp '(' len(2) . indx
*	outfile = cs 'getarg typ' ls typ ls '  disp' cs disp cs
	disp = v(cfp_b * v(disp))
*	outfile = cs ' getarg 12,13 disp' cs disp cs

	base = indx
*	outfile = '#quitting'; dump(3) :(end)
	indx = 					:(getarg.mem)

*	name(x), where name is in working section

getarg.c.14
getarg.c.15
	
*	outfile = cs 'getarg.14.15' ls txt cs ' disp ' disp ' val' ls val ls
	txt break('(') . disp '(' len(2) . indx	
	val = equ_value[disp]
	disp = differ(val) literal_ val
						:(getarg.mem)

*	signed integer

getarg.c.16 getarg = txt			:(getarg.done)

*	signed real

getarg.c.17 getarg = txt			:(getarg.done)



	txt = i.text(iarg)
	typ = i.type(iarg)
	
	output = lt(typ, 18) gt(typ.22) ' impossible type for getval ' typ
	eq(typ)					:f($(valcase[typ]))
	getarg = txt				:(getval.done)

getarg.c.18	;* =dlbl
	getarg = iv(substr(txt,2))		:(getarg.done)

getarg.c.19	;* *dlbl
	getarg = iv(cfp_b * v(substr(txt,2)))	:(getarg.done)
getarg.c.20	;* =name (data section)
getarg.c.21	;* =name (program section)
getarg.c.22	;* =elbl
        getarg =  '$' substr(txt,2)		:(getarg.done)

*	pnam, eqop
getarg.c.23
getarg.c.24 
	getarg = txt				:(getarg.done)
*	getarg = '$(' equ_value[txt] ')'	:(arop.done)
	getarg =  equ_value[txt]		:(arop.done)

*	ptyp, text, dtext
getarg.c.25
getarg.c.26
getarg.c.27 getarg = txt			:(getarg.done)
getarg.done
	i_suspend = 0

*	ne(x_trace) outline('; arg ' typ ls txt   ' -> ' getarg)
						:(return)
getarg.mem
	getarg = differ(base) disp '(' regmap[base] ')'		:s(getarg.done)	
	getarg = ident(indx) disp 				:s(getarg.done)
	getarg = ident(scale) ident(disp) '(' regmap[indx] ')'	:s(getarg.done)
	getarg = ident(disp) ident(scale) '(' regmap[indx] ')' 	:s(getarg.done)
	getarg = differ(disp) ident(scale) disp '(' regmap[indx]  ')'	:s(getarg.done)
	error('getarg');

-stitl getlea(iarg)
getlea

*	similar to getarg, but gets effective address this procedure only called if operand is in 
*	range 3-4 or 9-15 it generates code to load effective address of the argument to w0

	txt = i.text(iarg)
	typ = i.type(iarg)
	eq(typ)					:f($(leacase[typ]))

*	wlbl, clbl

getlea.c.3
getlea.c.4
	op(mov.w,reg.w0,literal_ txt)		:(lea.done)

*	(x), register indirect

getlea.c.9
	txt len(1) len(2) . reg
	op(mov.w,reg.w0,regmap[reg])		:(lea.done)

*	int(x)
*	dlbl(x)

getlea.c.12
getlea.c.13
	txt break('(') . disp '(' len(2) . reg
	op(mov.w,reg.w0,regmap[reg])
	op(add.w,reg.w0,iv(cfp_b * v(disp)))	:(lea.done)

*	name(x), where name is in working section

getlea.c.14
getlea.c.15
	txt break('(') . disp '(' len(2) . reg
	op(mov.w,reg.w0,regmap[reg])
	op(add.w,reg.w0,literal_ disp)		:(lea.done)
lea.done
	getlea = reg.w0				:(return)
-stitl iv(text)
iv
	iv = '$' v(text)				:(return)
-stitl memmem()t
memmem
*  memmem is called for those ops for which both operands may be
*  in memory, in which case, we generate code to load second operand
*  to pseudo-register w0, and then modify the second argument
*  to reference this register

  eq(ismem[i.type(i1)])				:s(return)
  eq(ismem[i.type(i2)])				:s(return)
*  here if memory-memory case, load second argument
  t = getarg(i2)
  i2 = w0_arg
  op(mov.w,reg.w0,t)				:(return)

-stitl outline(txt)
outline	
	outlines = outlines + 1		
	outfile = txt
						:(return)
-stitl prcent(n)
prcent prcent = 'prc_+cfp_b*' ( n - 1)	:(return)

-stitl outstmt(ostmt)label,opcode,op1,op2,op3,comment)
*  this module writes the components of the statement
*  passed in the argument list to the formatted .s file

outstmt	label = t.label(ostmt)
* clear label if definition already emitted
	label = ident(label, lastlabel)

outstmt1
	comment = t.comment(ostmt)
* ds suppress comments
 	comment = tcomment = comments =
 	:(outstmt2)
*  attach source comment to first generated instruction
	differ(comment)				:s(outstmt2)
	ident(tcomment)				:s(outstmt2)
	comment = tcomment; tcomment =
outstmt2
	opcode = t.opc(ostmt)
	op1 = t.op1(ostmt)
	op2 = t.op2(ostmt)
	op3 = t.op3(ostmt)
	differ(compress)			:s(outstmt3)
	stmtout = rpad( rpad(label,7) tab rpad(opcode,4) tab
.		  (ident(op1), op1
.			(ident(op2), ',' op2
.				(ident(op3), ',' op3))) ,27)
.       (ident(strip_comment,'y'), ' ' (ident(comment), ';') comment)
.						:(outstmt4)
outstmt3
	stmtout = label tab opcode tab
.		  (ident(op1), op1
.		    (ident(op2), ',' op2
.		      (ident(op3), ',' op3)))
.       (ident(strip_comment,'y'), tab (ident(comment), ';') comment)

**	send text to outfile

**
outstmt4
**
**	send text to output file if not null.

*	stmtout = replace(trim(stmtout),'$','_')
	stmtout = trim(stmtout)
	ident(stmtout)				:s(return)
	outline(stmtout)
	ntarget	= ntarget + 1

*  record code labels in table with delimiter removed.
	(ge(sectnow,5) differ(thislabel))	:f(return)
	label ? break(ls) . label		:f(return)
	labtab<label> = outlines		:(return)

-stitl  chktrace()
chktrace
	eq(i_trace)				:s(return)
	ne(i_suspend)				:s(return)
	clabel = inlabel
 	old_i_skip = i_skip
 	old_i_exec = i_exec
 	old_is_exec = is_exec
	i_skip = ident(inlabel,'s_aaa') 0

	is_exec = is_executable[incode]
	i_exec = ne(i_trace)  ident(inlabel, 's_aaa') 1
	i_exec = ne(i_trace) ge(sectnow,5) 1

 	i_skip  = differ(inlabel) differ(skip_on[inlabel]) 1
 	i_skip  = differ(inlabel) differ(skip_off[inlabel]) 0

	ne(i_skip)				:s(return)
	eq(i_exec)				:s(return)
	eq(is_exec)				:s(return)

*	ne(in_gcol)				:s(return)
chktrace.1
	genz()
						:(return)

genz
*	no trace if trace has been suspended
*	output = ne(i_suspend) 'i_suspend ' thisline
	i_count = i_count + 1
	gt(i_first) le(i_count,i_first)		:s(return)
	gt(i_limit)  gt(i_count, i_limit)	:s(return)

*	only trace an instruction once
*	gas doesn't allow string constants as macro arguments, so we generate the declaratin
*	for the descriptive string directly, then issue a macro invocation

	eq(nlines,nlast)			:s(return)
	nlast = nlines
	lbl = genlab()
	i_desc = '"' replace(thisline,'|', ' ') '"'
	op('.data')
	opl(lbl,'.asciz',i_desc)
	i_suspend = 1;			;* no need to trace the trace code.
*	op(mov.w,'trc_de',literal_ lbl)
	op('trc_',lbl)
	i_suspend = 0

						:(return)

-stitl prsarg(iarg)
prsarg	prsarg = minarg(0)
	iarg break(',') . l1 ',' rem . l2	:f(return)
	prsarg = minarg(convert(l1,'integer'),l2)	:(return)
-stitl readline()
*  this routine returns the next statement line in the input file
*  to the caller.  it never fails.  if there is no more input,
*  then a minimal end statement is returned.
*  comments are passed through to the output file directly.


readline readline = infile                      :f(rl02)
	nlines  = nlines + 1
	ident( readline )			:s(readline)
readline.0
	leq( substr(readline,1,1 ),';' )       	:f(rl01)
	i_skip = ident(readline,';z+') 0	:s(readline)
	i_skip = ident(readline,';z-') 1	:s(readline)
* force skip of full line comments
	:(readline)

*  only print comment if requested.

	ident(strip_comment,'n')		:f(readline)
        readline len(1) = ';'
	outlines = outlines + 1               :(readline)

*  here if not a comment line

rl01	
*  find out why need to add 2 here
*  add 2 since need to account for this line and one that will follow
	ne(x_trace) outline(';:' outlines + 2 ls tab readline)
					:(return)

*  here on eof

rl02    readline = '       end'
						:(rl01)
-stitl ripinit()
*  initialize rip mode, allocating the table used to save allocated locations
ripinit
	riptable = table(500)
						:(return)

-stitl tblini(str)
*  this routine is called to initialize a table from a string of
*  index/value pairs.

tblini   pos     = 0

*  count the number of "[" symbols to get an assessment of the table
*  size we need.

tin01   str     (tab(*pos) '[' break(']') *?(cnt = cnt + 1) @pos)
.						:s(tin01)

*  allocate the table, and then fill it. note that a small memory
*  optimisation is attempted here by trying to re-use the previous
*  value string if it is the same as the present one.

	tblini   = table(cnt)
tin02   str     (break('[') $ index len(1) break(']') $ val len(1)) =
.						:f(return)
	val     = convert( val,'integer' )
	val     = ident(val,lastval) lastval
	lastval = val
	tblini[index] = val			:(tin02)
-stitl v(text)
*	return value of text, as integer
v
	ident(text)				:s(v.err)
	v = integer(text) +text			:s(return)
	v = differ(equ_value[text]) +equ_value[text]	:s(return)
v.err
	outfile = cs ' #error v value not found' ls  text ls
	dump(3) :(end)

-stitl generators

-stitl	getvalue(text)
getvalue
	integer(text)				:s(getvalue.done)
	text = equ_value[text]
	integer(text)				:s(getvalue.done)
	error('expecting integer, got ' text )
getvalue.done
	getvalue = +text			:(return)
	
ifreg	ge(i.type(iarg),7) le(i.type(iarg),8)
.						:f(freturn)s(return)

g.flc
	t1 = reglow[i.text(i1)]
	t2 = genlab()
*	i_suspend = 1
	op(cmp.b,t1,"'A'")
	op('jb', t2 )
	op(cmp.b,t1,"'Z'")
	op('ja', t2)
	op(add.b,t1,literal_ '32')
        opl(t2)
*	i_suspend = 0
						:(opdone)

g.mov
        i.src = i2; i.dst = i1
	t.src = i.text(i.src); t.dst = i.text(i.dst)
	ident(t.src,'(xl)+')			:s(mov.xlp)
	ident(t.src,'(xt)+')			:s(mov.xtp)
	ident(t.src,'(xs)+')			:s(mov.xsp)
	ident(t.dst,'(xr)+')			:s(mov.xrp)
	ident(t.dst,'-(xs)')			:s(mov.2)
	memmem()
	op(mov.w,getarg(i1),getarg(i2))
						:(opdone)
mov.xtp
mov.xlp
	ident(t.dst,'(xr)+') op(movs.w)	:s(opdone)
	op(lods.w)
	ident(t.dst,'-(xs)') op(push.w,reg.w0)	:s(opdone)
	op(mov.w,getarg(i.dst),reg.w0)		:(opdone)
mov.xsp
	ident(t.dst,'(xr)+')		:s(mov.xsprp)
	op(pop.w,getarg(i.dst))		:(opdone)
mov.xsprp op(pop.w,reg.w0)
	op(stos.w)				:(opdone)
mov.xrp op(mov.w,reg.w0,getarg(i.src))
	op(stos.w)				:(opdone)
mov.2
	op(push.w,getarg(i.src))		:(opdone)

* odd/even tests.  if w reg, use low byte of register.
g.bod	reg = getarg(i1)
 	reg = eq(i.type(i1),8) reglow[i.text(i1)]
	op(test.b,reg,iv(1))
	op('jne',getarg(i2))			:(opdone)

g.bev	reg = getarg(i1)
	reg = eq(i.type(i1),8) reglow[i.text(i1)]
	op(test.b,reg,iv(1))
	op('je',getarg(i2))
						:(opdone)

g.brn   op('jmp',getarg(i1))			:(opdone)

g.bsw	reg = getarg(i1)
	t2 = genlab()
	i_suspend = 1
	ident(i.text(i3))			:s(g.bsw1)
	op(cmp.w,reg,iv(getarg(i2)))
	op('jge',getarg(i3))
* here after default case.
g.bsw1	
	ne(rip_mode)				:s(g.bsw.2)	
*	op('jmp', 'm(' t2 '+' reg '*cfp_b)') :(g.bsw.3)
	outfile = cs 'g.bsw '  t2 '(,' reg ',' cfp_b ')'
	op('jmp','*'  t2 '(,' reg ',' cfp_b ')' )	:(g.bsw.3)
g.bsw.2
* in rip_mode, need to generate location to reference destination
	lbl = genrip()
        op('.data')
	opl(lbl,'.quad',t2 )
	op('.text')
	op(mov.w,reg.w0,t1)
	op(shl.w,reg.w0,iv(log_cfp_b))
	op(add.w,reg.w0,lbl)
	op('jmp', '*' w0)
g.bsw.3
	op('.data')
        opl(t2)
	i_suspend = 0
						:(opdone)

g.iff   op('.quad',getarg(i2))              :(opdone)

g.esw
        op('.text')			:(opdone)
g.ent

*  entry points are stored in byte before program entry label
*  last arg is optional, in which case no initial '.byte' need be
*  issued. we force odd alignment so can distinguish entry point
*  addresses from block addresses (which are always even).

*  note that this address of odd/even is less restrictive than
*  the minimal definition, which defines an even address as being
*  a multiple of cfp_b (4), and an odd address as one that is not
*  a multiple of cfp_b (ends in 1, 2, or 3).  the definition here
*  is a simple odd/even, least significant bit definition.
*  that is, for us, 1 and 3 are odd, 2 and 4 are even.


	t1 = i.text(i1)
*        op('.align',2)
	outline(tab '.align' tab '2')
	differ(t1)				:s(g.ent.1)
	outline(tab 'nop')
						:(g.ent.2)
g.ent.1
	outline(tab '.byte' tab	t1)

g.ent.2
	opl(thislabel)
*  note that want to attach label to last instruction
*	t1 = cstmts[cstmts.n]
*	t.label(t1) = tlabel
*	cstmts[cstmts.n] = t1
*  here to see if want label made public
	thislabel ? rtab(1) . thislabel
        (differ(pubtab[thislabel]), differ(debug)) op('.global',thislabel)
	thislabel =				:(opdone)

g.bri	op('jmp','*' getarg(i1))			:(opdone)

g.lei	reg = regmap[i.text(i1)]
	op(mov.w,reg.w0,reg)
	op(dec.w,reg.w0)
	op(mov.b,reg.al,'(' reg.w0 ')')
	op(movz.bq,reg,reg.al)
						:(opdone)
g.jsr
	jsr_proc = getarg(i1)
	op('call',jsr_proc)
*	get count of following ppm statements
	jsr_count = ppm_cases[jsr_proc]
	eq(jsr_count)				:s(opdone)
	i_suspend = 1
	jsr_calls = jsr_calls +  1
	jsr_label = 'call_' jsr_calls
	jsr_label_norm = jsr_label
	op(dec.w,rcode)
	op('js',jsr_label_norm)
	i_suspend = 0

*	generate branch around for ppms that will follow
*	take the branch if normal return (eax==0)
						:(opdone)

g.err
g.ppm

*  here with return code in rcode. it is zero for normal return
*  and positive for error return. decrement the value.
*  if it is negative then this is normal return. otherwise,
*  proceed decrementing rcode until it goes negative,and then
*  take the appropriate branch.

	t1 = getarg(i1)

*  branch to next case if rcode code still not negative.
	ident(incode,'ppm')			:s(g.ppm.loop)
	count.err =  count.err + 1
	errfile =   i.text(i1) ' ' i.text(i2)
	max.err = gt(t1,max.err) t1
						:(g.ppm.loop)

g.ppm.loop.next
	opl(lab_next)
 	jsr_count = jsr_count - 1
 	i_suspend = eq(jsr_count) 0
	eq(jsr_count) opl(jsr_label_norm) :(opdone)
g.ppm.loop
	lab_next = genlab()
	op(dec.w,rcode)
	op('jns',lab_next)
	ident(incode,'ppm')			:s(g.ppm.loop.ppm)
*  here if error exit via exi. set rcode to exit code and jump
*  to error handler with error code in rcode
g.ppm.loop.err
	op(mov.w,rcode,literal_  +t1)
	op('jmp','err_')
						:(g.ppm.loop.next)
g.ppm.loop.ppm
*	check each ppm case and take branch if appropriate
	ident(i.text(i1))			:s(g.ppm.2)
	count.ppm = count.ppm + 1
	op('jmp',	getarg(i1))
						:(g.ppm.loop.next)

g.ppm.2
*  a ppm with no arguments, which should never be executed, is
*  translated to err 299,internal logic error: unexpected ppm branch
	t1 = 299
	errfile =  t1 ' internal logic error: unexpected ppm branch'
						:(g.ppm.loop.err)

g.prc

*  generate public declaration
*	t1 = thislabel
*	t1 ? rtab(1) . t1
*	op()
*	op('.global',t1)
*  nop needed to get labels straight
	prc.args = getarg(i2)
	ppm_cases[thislabel] = i.text(i2)
	thislabel =
	max_exi = gt(prc.args,max_exi) prc.args
	prc.type = i.text(i1)			:($('g.prc.' prc.type))
g.prc.e
g.prc.r						:(opdone)

g.prc.n
*  store return address in reserved location
	prc.count = prc.count + 1
	op(pop.w, prcent(prc.count))		:(opdone)

g.exi
        t1 = getarg(i1); t2 = prc.type; t3 = i.text(i1)
*  if type r or e, and no exit parameters, just return
 	differ(t2,'n') eq(prc.args)	op('ret')	:s(opdone)
        t3 = ident(t3) '0'
    	op(mov.w,rcode,literal_ +t3)
	ident(t2,'n')				:s(g.exi.1)
	op('ret')				:(opdone)
g.exi.1

	op(mov.w,reg.w0,prcent(prc.count))
	op('jmp','*' reg.w0)
						:(opdone)
g.enp   op()					:(opdone)

g.erb
	errfile =  i.text(i1) ' ' i.text(i2)
*	set rcode to error code and branch to error handler
	op(mov.w, rcode,literal_   +(i.text(i1)))
 	op('jmp','err_')
						:(opdone)


g.icv   op(inc.w,getarg(i1))    :(opdone)
g.dcv   op(dec.w,getarg(i1))    :(opdone)

g.zer	ident(i.text(i1),'(xr)+') op(xor.w,reg.w0,reg.w0)
+		op(stos.w)			:s(opdone)
	ifreg(i1)				:s(g.zer1)
	ident(i.text(i1),'-(xs)')		:s(g.zer.xs)
	op(xor.w,reg.w0,reg.w0)
	op(mov.w,getarg(i1),reg.w0)		:(opdone)
g.zer1	reg = getarg(i1)
	op(xor.w,reg,reg)			:(opdone)
g.zer.xs 
	op(xor.w,reg.w0,reg.w0)
	op(push.w,reg.w0)			:(opdone)

g.mnz   
	op(xor.w,reg.w0,reg.w0)
	op(inc.w,reg.w0)
	op(mov.w,getarg(i1),reg.w0)		:(opdone)

g.ssl
g.sss
g.rtn
	op()					:(opdone)

g.add	memmem()
	op(add.w,getarg(i1),getarg(i2))	:(opdone)

g.sub	memmem()
	op(sub.w,getarg(i1),getarg(i2))	:(opdone)

g.ica   op(add.w,getarg(i1),iv(cfp_b))	:(opdone)

g.dca   op(sub.w,getarg(i1),iv(cfp_b))	:(opdone)

g.beq
g.bne
g.bgt
g.bge
g.blt
g.ble
g.blo
g.bhi

*  these operators all have two operands, memmem may apply
*  issue target opcode by table lookup.

	memmem()
	reg = branchtab[incode]
	op(cmp.w,getarg(i1),getarg(i2))
	op(branchtab[incode],getarg(i3))
.						:(opdone)

g.bnz
	ifreg(i1)				:s(g.bnz1)
	op(xor.w,reg.w0,reg.w0)
*        op(cmp.w, getarg(i1) ,reg.w0)
        op(cmp.w, reg.w0,getarg(i1))
	op('jnz',getarg(i2))
						:(opdone)
g.bnz1
	reg = getarg(i1)
	op(or.w,reg,reg)
	op('jnz',getarg(i2))
						:(opdone)

g.bze   ifreg(i1)				:s(g.bze1)
	op(xor.w,reg.w0,reg.w0)
        op(cmp.w, getarg(i1),reg.w0)
	op('jz',getarg(i2))
						:(opdone)
g.bze1
	reg = getarg(i1)
	op(or.w,reg,reg)
	op('jz',getarg(i2))			:(opdone)

g.lct

*  if operands differ must emit code

	differ(i.text(i1),i.text(i2))		:s(g.lct.1)
*  here if operands same. emit no code if no label, else emit null
	:(opdone)
	ident(thislabel)			:s(opnext)
	op()					:(opdone)

g.lct.1	op(mov.w,getarg(i1),getarg(i2))	:(opdone)

g.bct
*  can issue loop if target register is cx.
	reg = getarg(i1)
	t2 = getarg(i2)
	:(g.bct2)
	ident(reg,reg.wa)				:s(g.bct1)
g.bct2
	op(dec.w,reg)
	op('jnz',t2)				:(opdone)
g.bct1
	op('loop',t2)			:(opdone)

g.aov
	op(add.w,getarg(i2),getarg(i1))
	op('jc',getarg(i3))
						:(opdone)
g.lcp
g.lcw
g.scp
	op(op_,getarg(i1))			:(opdone)

*  use cp for code pointer.
	op(mov.w,reg.cp,getarg(i1))
						:(opdone)
g.icp
	op(op_)				:(opdone)
g.ldi
g.sti
g.adi
g.mli
g.sbi
g.dvi
g.rmi
	op(op_,getarg(i1))			:(opdone)

g.ngi
	op(op_)				:(opdone)

g.ino
g.iov
	op(op_,getarg(i1))			:(opdone)


g.ieq	jop = 'je'				:(op.cmp)
g.ige	jop = 'jge'				:(op.cmp)
g.igt	jop = 'jg'				:(op.cmp)
g.ile	jop = 'jle'				:(op.cmp)
g.ilt	jop = 'jl'				:(op.cmp)
g.ine	jop = 'jne'				:(op.cmp)
op.cmp
	op('sti_',reg.w0)
	op(or.w,reg.w0,reg.w0)
	op(jop,getarg(i1))			:(opdone)

*  real operations

g.itr	op('call','itr_')	:(opdone)

g.rti	op('rti_')
	eq(i.type(i1))				:s(opdone)
*  here if label given, branch if real too large
        op('jc',getarg(i1))                 :(opdone)

g.ldr
g.str
g.adr
g.sbr
g.mlr
g.dvr
	getlea(i1)				;* get address to w0
	op('call',op_)
						:(opdone)
g.ngr
g.atn
g.chp
g.cos
g.etx
g.lnf
g.sin
g.sqr
g.tan
	op('call',op_)
						:(opdone)

g.rov
g.rno	op(op_,getarg(i1))			:(opdone)
*g.rno	t1 = 'jno'				:(g.rov1)
*g.rov	t1 = 'jo'
*g.rov1  op('call','ovr_')
	op(t1,getarg(i1))			:(opdone)

g.req	jop = 'je'				:(g.r1)
g.rne	jop = 'jne'				:(g.r1)
g.rge	jop = 'jge'				:(g.r1)
g.rgt	jop = 'jg'				:(g.r1)
g.rle	jop = 'jle'				:(g.r1)
g.rlt	jop = 'jl'
g.r1	op('call','cpr_')
	op(mov.b,reg.al,'reg_fl')
	op(or.b,reg.al,reg.al)
	op(jop,getarg(i1))			:(opdone)

g.plc
g.psc
*  last arg is optional.  if present and a register or constant, then  use lea instead.

	reg = getarg(i1)
	typ = i.type(i2)
	(ifreg(i2), ge(typ,1) le(typ,2)) op(lea.w,reg, cfp_f '(' reg ',' getarg(i2) ')') :s(opdone)
	op(add.w,reg,iv(cfp_f))
	eq(i.type(i2))				:s(opdone)

*  here if d_offset_(given (in a variable), so add it in.

	op(add.w,reg,getarg(i2))		:(opdone)

*  lch requires separate cases for each first operand possibility.

g.lch
*	outfile = cs 'g.lch' ls i.text(i1) ls i.text(i2) ls

*	clear reg, load character to %al, then or with target
	opv = i.text(i2)
	reg = getarg(i1)

	op(xor.w,reg.w0,reg.w0)
	op(mov.b,reg.al,getarg(i2))
	op(mov.w,reg,reg.w0)
		:(opdone)

*  see if predecrement needed.
	leq('-',substr(opv,1,1))			:f(g.lch.1)
	opv break('(') len(1) len(2) . t3
	op(dec.w,regmap[t3])
g.lch.1
	opv break('(') len(1) len(2) . t3
	op(xor.w,reg.w0,reg.w0)
	op(mov.b,reg.al,'(' regmap[t3] ')')
	op(mov.w,reg,reg.w0)


*  see if postincrement needed.
	opv rtab(1) '+'				:f(g.lch.2)
	op(inc.w,regmap[t3])
g.lch.2						:(opdone)

g.sch
*	outfile = cs 'g.sch' ls i.text(i1) ls i.text(i2) ls
	reg = getarg(i1)
	opv = i.text(i2)
*	
	op(mov.w,reg.w0,reg)
	op(mov.b,getarg(i2),reg.al)
		:(opdone)

	op(and.w,reg,reg.w0)
	op(mov.b,reg.al,getarg(i2))
	:(opdone)


	eq(i.type(i1),8)			:s(g.sch.w)
	:(g.sch.0)
	ident(opv,'(xr)+')			:f(g.sch.0)

*  here if can use stos.

	op(mov.b,reg.al,getarg(i1))
	op(stos.b)				:(opdone)

g.sch.0
	leq('-',substr(opv,1,1))			:f(g.sch.1)
	opv break('(') len(1) len(2) . t3
	op(dec.w,regmap[t3])
	op(dec.w,regmap[t3])
g.sch.1
	opv break('(') len(1) len(2) . t3
	op(mov.w,reg.w0,reg,)
	op(mov.b, '(' regmap[t3] ')' ,reg.al)
*  see if postincrement needed.
	opv rtab(1) '+'				:f(g.sch.2)
	op(inc.w,regmap[t3])
g.sch.2						:(opdone)
g.sch.w

*  here if moving character from work register, convert reg to name of low part.

	regl = reglow[i.text(i1)]

	ident(opv,'(xl)')			:s(g.sch.w.xl)
	ident(opv,'-(xl)')			:s(g.sch.w.pxl)
	ident(opv,'(xl)+')			:s(g.sch.w.xlp)
	ident(opv,'(xr)')			:s(g.sch.w.xr)
	ident(opv,'-(xr)')			:s(g.sch.w.pxr)
	ident(opv,'(xr)+')			:s(g.sch.w.xrp)
g.sch.w.xl
	op(mov.b,'(' reg.xl ')',regl)		:(opdone)
g.sch.w.pxl
	op(dec.w, '(' reg.xl ')')
	op(mov.b, '(' reg.xl ')',regl)	:(opdone)

g.sch.w.xlp
	op(mov.b,'(' reg.xl ')',regl)
	op(inc.w, '(' reg.xl ')')
						:(opdone)
g.sch.w.xr
	op(mov.b,'(' reg.xr ')',regl)		:(opdone)

g.sch.w.pxr
	op(dec.w, '(' reg.xr ')')
	op(mov.b,'(' reg.xr ')',regl)	:(opdone)
g.sch.w.xrp
	op(mov.b,reg.al,regl)
	op(stos.b)				:(opdone)

g.csc  	ident(thislabel)			:s(opnext)
	op()					:(opdone)

g.ceq
	memmem()
	op(cmp.w,getarg(i1),getarg(i2))
	op('je',getarg(i3))
						:(opdone)

g.cne   memmem()
	op(cmp.w,getarg(i1),getarg(i2))
	op('jnz',getarg(i3))
						:(opdone)

g.cmc
	op('repe',cmps.b)
	op(xor.w,reg.xl,reg.xl)
	op(mov.w,reg.xr,reg.xl)
	t1 = getarg(i1)
	t2 = getarg(i2)
	(ident(t1,t2) op('jnz',t1))		:s(opdone)
	op('ja',t2)
	op('jb',t1)				:(opdone)

g.trc
	op(xchg.w,reg.xl,reg.xr)

	opl((t1 = genlab()), movz.bq, reg.w0, '(' reg.xr ')')

	op(mov.b,reg.al, '(' reg.xl ',' reg.w0 ')')
	op(stos.b)
*	op('loop',t1)
	op(dec.w,reg.wa)
	op('jnz',t1)
	op(xor.w,reg.xl,reg.xl)
	op(xor.w,reg.xr,reg.xr)
						:(opdone)

g.anb   op(and.w,getarg(i1),getarg(i2))	:(opdone)
g.orb   op(or.w,getarg(i1),getarg(i2))	:(opdone)
g.xob   op(xor.w,getarg(i1),getarg(i2))	:(opdone)
g.cmb   op(not.w,getarg(i1))			:(opdone)

g.rsh
	op(shr.w,getarg(i1),iv(getarg(i2)))	:(opdone)

g.lsh
	op(shl.w,getarg(i1),iv(getarg(i2)))	:(opdone)

g.rsx
	error('rsx not supported')
g.lsx
	error('lsx not supported')

g.nzb	ifreg(i1)				:s(g.nzb1)
	op(xor.w,reg.w0,reg.w0)
	op(cmp.w,getarg(i1),w0.reg)
	op('jnz',getarg(i2))
						:(opdone)
g.nzb1
	op(or.w,getarg(i1),getarg(i1))
	op('jnz',getarg(i2))
						:(opdone)

g.zrb	ifreg(i1)				:s(g.zrb1)
	op(xor.w,w0,w0)
	op(cmp.w,getarg(i1),w0.reg)
	op('jz',getarg(i2))
						:(opdone)
g.zrb1
	op(or.w,getarg(i1),getarg(i1))
	op('jz',getarg(i2))
						:(opdone)

g.zgb
	op('nop')				:(opdone)

g.zzz
 	op('zzz',getarg(i1))			:(opdone)

g.wtb   op(shl.w,getarg(i1),iv(log_cfp_b))	:(opdone)

g.btw   op(shr.w,getarg(i1),iv(log_cfp_b))	:(opdone)


g.mti	
	outfile = cs 'g.mti' ls i.text(i1)
	ident(i.text(i1),'(xs)+')		:f(g.mti.1)
	op(pop.w,reg.w0)
	op('ldi_',reg.w0)			:(opdone)
g.mti.1
	op('ldi_',getarg(i1))		:(opdone)

g.mfi
*  last arg is optional
*  compare with cfp$m, branching if result negative
	eq(i.type(i2))				:s(g.mfi.1)
*  here if label given, branch if wc not in range (ie, negative)
	op('sti_',reg.w0)
	op(or.w,reg.w0,reg.w0)
	op('js',getarg(i2))
g.mfi.1
	ident(i.text(i1),'-(xs)')		:s(g.mfi.2)
	op('sti_',getarg(i1))		:(opdone)
g.mfi.2
	op('sti_',reg.w0)
	op(push.w,reg.w0)			:(opdone)

g.ctw
*  assume cfp_c chars per word
	reg = getarg(i1)
	op(add.w,reg,iv((cfp_b - 1) + cfp_b * v(i.text(i2))))
	op(shr.w,reg,iv(log_cfp_c))
					:(opdone)
g.ctb
	reg = getarg(i1)
	op(add.w,reg,iv((cfp_b - 1) + cfp_b * v(i.text(i2))))
	op(and.w,reg, iv(-cfp_b))
						:(opdone)
g.cvm	lbl = getarg(i1)
	op('sti_',reg.w0)

*	can't write 64-bit literal 10, so use storage location 'ten' defined in sys.asm.

	op(imul.w,reg.w0,ten)			
	op('jo',lbl)
	op(sub.w,reg.wb,'ch_d0')
	op(sub.w,reg.w0,reg.wb)
	op('ldi_',reg.w0)
	op('jo',t1)
						:(opdone)
g.cvd
	op('cvd_')				:(opdone)
g.mvc
*	use word move if character size is word size
*	if charsize is word size, convert character count to byte count for word move

	t1 = genlab()
	i_suspend = 1
	op('rep')
	op(movs.b)
	i_suspend = 0
						:(opdone)
	t1 = genlab()
*	Minimal's xl and xr registers correspond to the hardware 'source' and 'destination' registers
	opl(t1, mov.w,reg.al,reglow['xl'])
	op(mov.w,reglow['xr'],reg.al)
	op(inc.w, reg.xl)
	op(inc.w, reg.xr)
	op(dec.w,reg.wa)
	op('jnz', t1)
						:(opdone)

	t1 = genlab()
	opl(t1, lods.b)
	op(stos.b)
	op(dec.w, reg.wa)
	op('jnz',t1)
						:(opdone)
g.mvw
	i_suspend = 1
	op(shr.w,reg.wa,iv(log_cfp_b))
 	op('rep',movs.w)
	i_suspend = 0
						:(opdone)

g.mwb
	op(shr.w,reg.wa,iv(log_cfp_b))
	op('std')
	
	op(lea.w,reg.xl, '-' cfp_b '(' reg.xl ')')
	op(lea.w,reg.xr, '-' cfp_b '(' reg.xr ')')
 	genrep(movs.w)
	op('cld')				:(opdone)

	op('std')
	op(shr.w,reg.wa,iv(log_cfp_b))
	op('rep')
	op(movs.w)
	op('ctd')
						:(opdone)

g.mcb
*	use word move if character size is word size
	op('std')
	op(dec.w,reg.xl)
	op(dec.w,reg.xr)
 	genrep(movs.b)
	op('cld')
						:(opdone)
	op('std')
	op('rep')
	op(movs.b)
	op('cld')
						:(opdone)
genrep
*	generate equivalent of rep op loop
	l1 = genlab()
	l2 = genlab()
	opl(l1)
	op(or.w,reg.wa,reg.wa)
	op('jz',l2)
	op(op)
	op(dec.w,reg.wa)
	op('jmp',l1)
	opl(l2)
						:(return)

g.chk
	op('chk_')
	op(or.w,reg.w0,reg.w0);
	op('jne','sec06')			:(opdone)

decend
*  here at end of dic or dac to see if want label made public
	thislabel ? rtab(1) . thislabel
        differ(pubtab[thislabel]) op('.global',thislabel)
						:(opdone)

g.dac	t1 = i.type(i1)
        t2 = "" ;*(le(t1,2) "", le(t1,4) "d_offset_(", le(t1,6) "d_offset_(", "")
        opl(thislabel,'.quad',t2 i.text(i1))
						:(decend)
g.dic   opl(thislabel,'.quad',i.text(i1))
						:(decend)

g.drc
	op('.align',8)
	t1 = i.text(i1)
	t1 ? fence "+" = ""
        opl(thislabel,'.double',t1)
						:(opdone)
*  note that want to attach label to last instruction
	t.label(cstmts[cstmts.n]) = thislabel
	thislabel =				:(opdone)

g.dtc
*  change first and last chars to " (assume / used in source)
	t1 = i.text(i1)
	t1 tab(1) rtab(1) . txt
        str = '"' txt '"'
	opl(thislabel,'.ascii',str)
	eq(remdr(size(txt),cfp_c))		:s(opdone)
	left = cfp_c - remdr(size(txt),cfp_c)
g.dtc.1
	eq(left)				:s(opdone)
*  append nulls to complete last word so length is multiple of word word
	op('.byte','0')
	left = left - 1				:(g.dtc.1)

g.dbc   opl(thislabel,'.quad',getarg(i1))
						:(opdone)
g.equ   
*	opl(thislabel,'equ',i.text(i1))
	op('.set',thislabel,v(thislabel))
						:(opdone)
g.exp
	ppm_cases[thislabel] = i.text(i1)
*	op('extern',thislabel)
	thislabel =				:(opdone)

g.inp
	ppm_cases[thislabel] = i.text(i2)
	prc.count1 = ident(i.text(i1),'n') prc.count1 + 1
+						:(opnext)

g.inr						:(opnext)

g.ejc	op('')				:(opdone)

g.ttl	op('')
						:(opdone)

g.sec	op('')
	sectnow = sectnow + 1			:($("g.sec." sectnow))

* procedure declaration section
g.sec.1 op('.text')
        op('.global','sec01')
        opl('sec01')             	:(opdone)

* definitions section
g.sec.2
        op(' .data')
        op('.global','sec02')
        opl('sec02')             	:(opdone)

* constants section
g.sec.3
        op(' .data')
        op('.global','sec03')
        opl('sec03')     		:(opdone)

* working variables section
g.sec.4 op('.global','esec03')
        opl('esec03')
        op(' .data')
        op('.global','sec04')
        opl('sec04')     
						:(opdone)

*  here at start of program section.  if any n type procedures,
*  put out entry-word block declaration at end of working storage
g.sec.5
*  emit code to indicate in code section
*  get direction set to up.
        op('.global','esec04')
        opl('esec04')
	opl('prc_','.fill', prc.count1 '0,0')
        op('.global','end_min_data')
        opl('end_min_data')
        op(' .text')
        op('.global','sec05')
        opl('sec05')     
*  enable tracing if desired
						:(opdone)

*  stack overflow section.  output exi__n tail code
g.sec.6
        op('.global','sec06')
        opl('sec06', 'nop')
				             :(opdone)

*  error section.  produce code to receive erb's
g.sec.7
        op('.global','sec07')
        opl('sec07')
	flush()
*  error section.  produce code to receive erb's

*	allow for some extra cases in case of max.err bad estimate
	n1 = max.err + 8
	opl('err_',xchg.w,reg.wa,rcode)
						:(opdone)


opdone	flush()					:(opnext)

*  here to emit bstmts, cstmts, astmts. attach input label and
*  comment to first instruction generated.

flush	eq(astmts.n) eq(bstmts.n) eq(cstmts.n)	:f(opdone1)

*  here if some statements to emit, so output single 'null' statement to get label
*  and comment field right.

	label = thislabel =
	outstmt(tstmt())			:(opdone.6)
opdone1	eq(bstmts.n)				:s(opdone.2)
	i = 1
opdone.1
	outstmt(bstmts[i])
	le(i = i + 1, bstmts.n)			:s(opdone.1)

opdone.2	eq(cstmts.n)			:s(opdone.4)
	i = 1
opdone.3
	outstmt(cstmts[i])
	le(i = i + 1, cstmts.n)			:s(opdone.3)

opdone.4	eq(astmts.n)			:s(opdone.6)
	i = 1
	ident(pifatal[incode])			:s(opdone.5)
*  here if post incrementing code not allowed
	error('post increment not allowed for op ' incode)
opdone.5	outstmt(astmts[i])
	le(i = i + 1, astmts.n)			:s(opdone.5)
opdone.6 astmts.n = bstmts.n = cstmts.n =	:(return)
flush_end

report
	output = lpad(num,7) tab text		:(return)


g.end
	&dump = 0
	ident(havehdr)				:s(g.end.2)
*  here to copy remaining part from hdr file
g.end.1	line = hdrfile				:f(g.end.2)
	ntarget = ntarget + 1
	outline(line)				:(g.end.1)
g.end.2

* here at end of code generation.

	endfile(1)
	endfile(2)
	endfile(3)
	report(i_trace,		'trace option')
        report(nlines,		'lines read')
        report(nstmts,		'statements processed')
        report(ntarget,		'target code lines produced')
	report(&stcount,	'spitbol statements executed')
        report(max.err,		'maximum err/erb number')
        report(prc.count1, 	'prc count')
        output  = '  ' gt(prc.count,prc.count1)
.	  'differing counts for n-procedures:'
.	  ' inp ' prc.count1 ' prc ' prc.count
        differ(nerrors) report(nerrors,'errors detected')

	errfile = '* ' max.err 'maximum err/erb number'
	errfile  = '* ' prc.count 'prc count'
.		differ(lasterror) '  the last error was in line ' lasterror

	&code   = ne(nerrors) 2001
        report(collect(), 'free words')
	report(time(),'execution time ms')
	:(end)
end
